variable "location" {
  default = "$REGION"
}

variable "cidr_all" {
  default = "$CIDR_ALL"
}

variable "cidr_nodes" {
  default = "$CIDR_NODES"
}

variable "cidr_pods" {
  default = "$CIDR_PODS"
}

variable "cidr_services" {
  default = "$CIDR_SERVICES"
}

variable "cidr_db" {
  default = "$CIDR_DB"
}

variable "cidr_redis" {
  default = "$CIDR_REDIS"
}


# Configure providers

terraform {
  required_providers {
    azurerm = {
      source  = "hashicorp/azurerm"
      version = "~> 4.39"
    }
    azuread = {
      source = "hashicorp/azuread"
      version = "~> 3.5"
    }
    tls = {
      source  = "hashicorp/tls"
      version = "~> 4.1"
    }
    random = {
      source = "hashicorp/random"
      version = "~> 3.7"
    }
  }
}

provider "azurerm" {
  resource_provider_registrations = "core"
  resource_providers_to_register = [
    "Microsoft.Network",
    "Microsoft.Compute",
    "Microsoft.DBforPostgreSQL",
    "Microsoft.Cache",
    "Microsoft.Storage",
    "Microsoft.ContainerService",
  ]
  features {
    key_vault {
      purge_soft_delete_on_destroy    = true
      recover_soft_deleted_key_vaults = true
    }
  }

  subscription_id = "$ARM_SUBSCRIPTION_ID"
}

data "azuread_client_config" "current" {}

data "azurerm_subscription" "main" {}

# Resource group

resource "azurerm_resource_group" "main" {
  name = "${NAME}"
  location = var.location
}

# Network ==================================================

resource "azurerm_virtual_network" "main" {
  name = "${NAME}"
  resource_group_name = azurerm_resource_group.main.name
  location = azurerm_resource_group.main.location
  address_space = [
    var.cidr_all,
  ]
}

# Subnets

resource "azurerm_subnet" "workers" {
  name                 = "${NAME}-workers"
  resource_group_name  = azurerm_resource_group.main.name
  virtual_network_name = azurerm_virtual_network.main.name
  address_prefixes     = [var.cidr_nodes]
}

resource "azurerm_subnet" "database" {
  name                 = "${NAME}-db"
  resource_group_name  = azurerm_resource_group.main.name
  virtual_network_name = azurerm_virtual_network.main.name
  address_prefixes     = [var.cidr_db]
  service_endpoints    = ["Microsoft.Storage"]

  private_endpoint_network_policies = "Enabled"

  delegation {
    name = "fs"
    service_delegation {
      name = "Microsoft.DBforPostgreSQL/flexibleServers"
      actions = [
        "Microsoft.Network/virtualNetworks/subnets/join/action",
      ]
    }
  }
}

resource "azurerm_subnet" "redis" {
  name = "${NAME}-redis"
  resource_group_name = azurerm_resource_group.main.name
  virtual_network_name = azurerm_virtual_network.main.name
  address_prefixes = [var.cidr_redis]
  private_endpoint_network_policies = "Enabled"
}

# NAT Gateway

resource "azurerm_nat_gateway" "main" {
  name = "${NAME}"
  location = azurerm_resource_group.main.location
  resource_group_name = azurerm_resource_group.main.name
  sku_name = "Standard"
}

resource "azurerm_subnet_nat_gateway_association" "workers" {
  subnet_id = azurerm_subnet.workers.id
  nat_gateway_id = azurerm_nat_gateway.main.id
}

resource "azurerm_public_ip" "main" {
  name = "${NAME}-nat-gateway"
  location = azurerm_resource_group.main.location
  resource_group_name = azurerm_resource_group.main.name
  allocation_method = "Static"
  sku = "Standard"
}

resource "azurerm_nat_gateway_public_ip_association" "main" {
  nat_gateway_id = azurerm_nat_gateway.main.id
  public_ip_address_id = azurerm_public_ip.main.id
}



# Application principals ======================================

# DNS principal

resource "azuread_application" "dns" {
  display_name = "${NAME}-dns"
  owners       = [data.azuread_client_config.current.object_id]
}

resource "azuread_service_principal" "dns" {
  client_id                    = azuread_application.dns.client_id
  app_role_assignment_required = false
  owners                       = [data.azuread_client_config.current.object_id]
  use_existing = true
}

output "client_id_dns" {
  value = azuread_application.dns.client_id
}

# Assistant principal

resource "azuread_application" "assistant" {
  display_name = "${NAME}-assistant"
  owners       = [data.azuread_client_config.current.object_id]
}

output "client_id_assistant" {
  value = azuread_application.assistant.client_id
}

# Studio principal

resource "azuread_application" "studio" {
  display_name = "${NAME}-studio"
  owners       = [data.azuread_client_config.current.object_id]
}

output "client_id_studio" {
  value = azuread_application.studio.client_id
}


# Storage containers ============================================


resource "azurerm_storage_account" "main" {
  name                     = "${NAME}"
  resource_group_name      = azurerm_resource_group.main.name
  location                 = azurerm_resource_group.main.location
  account_tier             = "Standard"
  account_replication_type = "ZRS"
}

# Model storage container

resource "azurerm_storage_container" "assistant" {
  name                  = "${ASSISTANT_STORAGE_CONTAINER}"
  storage_account_id    = azurerm_storage_account.main.id
  container_access_type = "private"
}

# Studio storage container

resource "azurerm_storage_container" "studio" {
  name                  = "${STUDIO_STORAGE_CONTAINER}"
  storage_account_id    = azurerm_storage_account.main.id
  container_access_type = "private"
}

# Key Vault ==============================================

resource "azurerm_key_vault" "main" {
  name                        = "${NAME}"
  location                    = azurerm_resource_group.main.location
  resource_group_name         = azurerm_resource_group.main.name
  enabled_for_disk_encryption = true
  tenant_id                   = data.azuread_client_config.current.tenant_id
  soft_delete_retention_days  = 7
  purge_protection_enabled    = false

  sku_name = "standard"

  access_policy {
    tenant_id = data.azuread_client_config.current.tenant_id
    object_id = data.azuread_client_config.current.object_id
    secret_permissions = ["Get", "List", "Set", "Delete", "Recover", "Backup", "Restore", "Purge"]
  }
}



# Database ===================================

# DB Private DNS

resource "azurerm_private_dns_zone" "db" {
  name                = "rasa-${NAME}.postgres.database.azure.com"
  resource_group_name = azurerm_resource_group.main.name
}

resource "azurerm_private_dns_zone_virtual_network_link" "example" {
  name                  = "${NAME}-db"
  private_dns_zone_name = azurerm_private_dns_zone.db.name
  virtual_network_id    = azurerm_virtual_network.main.id
  resource_group_name   = azurerm_resource_group.main.name
  depends_on            = [azurerm_subnet.database, azurerm_private_dns_zone.db]
}

# Root password

resource "random_password" "pg_main_pw" {
  length = 16
  override_special = "@!#-+:"
}

output "pg_main_pw" {
  value = random_password.pg_main_pw.result
  sensitive = true
}

resource "azurerm_key_vault_secret" "pg_main_pw" {
  name         = "${NAME}"
  value        = random_password.pg_main_pw.result
  key_vault_id = azurerm_key_vault.main.id
}

# DB Server

resource "azurerm_postgresql_flexible_server" "main" {
  name = "${NAME}"
  location = azurerm_resource_group.main.location
  resource_group_name = azurerm_resource_group.main.name

  delegated_subnet_id = azurerm_subnet.database.id
  private_dns_zone_id = azurerm_private_dns_zone.db.id

  public_network_access_enabled = false

  administrator_login = "postgres"
  administrator_password = random_password.pg_main_pw.result

  authentication {
    active_directory_auth_enabled = false
    password_auth_enabled = true
  }

  sku_name = "MO_Standard_E2ds_v4"
  version = 16

  auto_grow_enabled = true
  storage_mb = 262144
  storage_tier = "P30"

  backup_retention_days = 7
  geo_redundant_backup_enabled = true

  high_availability {
    mode = "SameZone"
  }

  lifecycle {
    ignore_changes = [
      zone,
      high_availability[0].standby_availability_zone
     ]
  }
}

resource "azurerm_postgresql_flexible_server_configuration" "main_tls_off" {
  name      = "require_secure_transport"
  server_id = azurerm_postgresql_flexible_server.main.id
  value     = "off"
}

output "db_host" {
  value = azurerm_postgresql_flexible_server.main.fqdn
}


# Redis =======================================================

# Private DNS Zone

resource "azurerm_private_dns_zone" "redis" {
  name                = "privatelink.redis.cache.windows.net"
  resource_group_name = azurerm_resource_group.main.name
}

resource "azurerm_private_dns_zone_virtual_network_link" "redis_vnet_link" {
  name                  = "${NAME}-redis"
  resource_group_name   = azurerm_resource_group.main.name
  private_dns_zone_name = azurerm_private_dns_zone.redis.name
  virtual_network_id    = azurerm_virtual_network.main.id
  depends_on            = [azurerm_subnet.redis, azurerm_private_dns_zone.redis]
}

# Redis server

resource "azurerm_redis_cache" "main" {
  name = "${NAME}"
  resource_group_name = azurerm_resource_group.main.name
  location = azurerm_resource_group.main.location

  capacity = 1
  family = "P"
  sku_name = "Premium"

  public_network_access_enabled = false

  replicas_per_master = 1

  patch_schedule {
    day_of_week = "Saturday"
    start_hour_utc = 22
  }

  redis_configuration {
    authentication_enabled = true
    active_directory_authentication_enabled = false
  }
}

# Private endpoint

resource "azurerm_private_endpoint" "main" {
  name                = "${NAME}-redis"
  resource_group_name = azurerm_resource_group.main.name
  location            = azurerm_resource_group.main.location
  subnet_id           = azurerm_subnet.workers.id

  private_dns_zone_group {
    name = "privatednsrediszonegroup"
    private_dns_zone_ids = [
      azurerm_private_dns_zone.redis.id,
    ]
  }

  private_service_connection {
    name                           = "${NAME}-redis"
    private_connection_resource_id = azurerm_redis_cache.main.id
    is_manual_connection           = false
    subresource_names              = ["redisCache"]
  }
}

output "redis_host" {
  value = azurerm_private_endpoint.main.private_dns_zone_configs.0.record_sets.0.fqdn
}

output "redis_pw" {
  value = azurerm_redis_cache.main.primary_access_key
  sensitive = true
}
output "redis_pw_secondary" {
  value = azurerm_redis_cache.main.secondary_access_key
  sensitive = true
}


# Public DNS zone ========================================

resource "azurerm_dns_zone" "main" {
  name = "${DOMAIN}"
  resource_group_name = azurerm_resource_group.main.name
}

output "dns_name_servers" {
  value = azurerm_dns_zone.main.name_servers
}

resource "azurerm_role_assignment" "dns" {
  scope                = data.azurerm_subscription.main.id
  role_definition_name = "DNS Zone Contributor"
  principal_id         = resource.azuread_service_principal.dns.object_id
}



# K8S ====================================================

resource "tls_private_key" "ssh" {
  algorithm = "RSA"
  rsa_bits  = 2048
}

resource "azurerm_key_vault_secret" "k8s_ssh_private_key" {
  name         = "${NAME}-k8s-ssh-private-key"
  value        = tls_private_key.ssh.private_key_openssh
  key_vault_id = azurerm_key_vault.main.id
}

resource "azurerm_key_vault_secret" "k8s_ssh_public_key" {
  name         = "${NAME}-k8s-ssh-public-key"
  value        = tls_private_key.ssh.public_key_openssh
  key_vault_id = azurerm_key_vault.main.id
}

resource "azurerm_kubernetes_cluster" "main" {
  name                = "${NAME}"
  resource_group_name = azurerm_resource_group.main.name
  location            = azurerm_resource_group.main.location
  dns_prefix          = "rasa"

  oidc_issuer_enabled = true
  workload_identity_enabled = true

  lifecycle {
    ignore_changes = [
      default_node_pool[0].node_count
    ]
  }

  identity {
    type = "SystemAssigned"
  }

  linux_profile {
    admin_username = "azureuser"
    ssh_key {
      key_data = replace(tls_private_key.ssh.public_key_openssh, "\n", "")
    }
  }

  default_node_pool {
    name                   = "blue"
    vm_size                = "Standard_D4s_v3"
    auto_scaling_enabled   = true
    min_count              = 1
    max_count              = 6
    node_public_ip_enabled = false
    vnet_subnet_id = azurerm_subnet.workers.id

    upgrade_settings {
      drain_timeout_in_minutes      = 30
      max_surge                     = "40%"
      node_soak_duration_in_minutes = 0
    }
  }

  network_profile {
    network_plugin = "azure"
    network_policy = "calico"
    network_data_plane = "azure"
    network_plugin_mode = "overlay"
    load_balancer_sku = "standard"

    pod_cidr = var.cidr_pods
    service_cidr = var.cidr_services
    dns_service_ip = cidrhost(var.cidr_services, 2) // 1rst is reserved as the Kubernetes default service IP
  }

  maintenance_window {
    allowed {
      day = "Saturday"
      hours = [1,2,3,4,5,6,7,8]
    }
    allowed {
      day = "Sunday"
      hours = [1,2,3,4,5,6,7,8]
    }
  }

}


resource "azurerm_role_assignment" "network_contributor" {
  principal_id = azurerm_kubernetes_cluster.main.identity[0].principal_id
  scope = azurerm_subnet.workers.id
  role_definition_name = "Network Contributor"
}

output "k8s_issuer_url" {
  value = azurerm_kubernetes_cluster.main.oidc_issuer_url
}

output "k8s_api_url" {
  value = azurerm_kubernetes_cluster.main.fqdn
}

output "kubeconfig" {
  value = azurerm_kubernetes_cluster.main.kube_config_raw
  sensitive = true
}


# Workload Identify ========================================================

resource "azuread_application_federated_identity_credential" "cert_manager" {
  application_id = azuread_application.dns.id
  display_name   = "rasa-${NAME}-certmanager"
  description    = "Rasa ${NAME} CertManager"
  audiences      = ["api://AzureADTokenExchange"]
  issuer         = azurerm_kubernetes_cluster.main.oidc_issuer_url
  subject        = "system:serviceaccount:cert-manager:cert-manager"
}

resource "azuread_application_federated_identity_credential" "external_dns" {
  application_id = azuread_application.dns.id
  display_name   = "rasa-${NAME}-externaldns"
  description    = "Rasa ${NAME} ExternalDns"
  audiences      = ["api://AzureADTokenExchange"]
  issuer         = azurerm_kubernetes_cluster.main.oidc_issuer_url
  subject        = "system:serviceaccount:external-dns:external-dns"
}

resource "azuread_application_federated_identity_credential" "assistant" {
  application_id = azuread_application.assistant.id
  display_name   = "rasa-${NAME}-assistant"
  description    = "Rasa ${NAME} Assistant"
  audiences      = ["api://AzureADTokenExchange"]
  issuer         = azurerm_kubernetes_cluster.main.oidc_issuer_url
  subject        = "system:serviceaccount:${NAMESPACE}:assistant"
}

resource "azuread_application_federated_identity_credential" "studio" {
  application_id = azuread_application.studio.id
  display_name   = "rasa-${NAME}-studio"
  description    = "Rasa ${NAME} Studio"
  audiences      = ["api://AzureADTokenExchange"]
  issuer         = azurerm_kubernetes_cluster.main.oidc_issuer_url
  subject        = "system:serviceaccount:${NAMESPACE}:studio"
}